# Maintainer: Jed Brown <jed@59A2.org>
# Contributor: Jed Brown <jed@59A2.org>
# Contributor: George Eleftheriou <eleftg>

pkgname=mpich-intel
pkgver=3.2
pkgrel=1
pkgdesc="An improved implementation of the Message Passing Interface."
url="https://mpich.org"
arch=("any")
license=("custom")
replaces=(mpich2)
depends=('python2' 'gcc-fortran' 'libxml2' 'openssh' 'numactl' 'pciutils')
makedepends=('texlive-core' 'sowing')
optdepends=("java-environment")
install="${pkgname}.install"
source=("http://www.mpich.org/static/downloads/${pkgver}/mpich-${pkgver}.tar.gz"
	    "mpich-intel.profile")
sha256sums=('0778679a6b693d7b7caff37ff9d2856dc2bfc51318bf8373859bfa74253da3dc'
            '0671ed13455a6b857027c33e738e261ab1b4b41321b7a5434ebecb087cd6c7d6')
options=('!libtool')

build() {
  cd ${srcdir}/mpich-${pkgver}
  source /opt/intel/composerxe/linux/bin/compilervars.sh intel64 -platform linux
  # CFLAGS etc are normally written into the wrapper compilers.  This
  # gives surprising results, e.g. when the user wants to compile their
  # program without optimization.
  export MPICHLIB_CFLAGS="${CFLAGS}";      unset CFLAGS
  export MPICHLIB_CXXFLAGS="${CXXFLAGS}";  unset CXXFLAGS
  export MPICHLIB_CPPFLAGS="${CPPFLAGS}";  unset CPPFLAGS
  export MPICHLIB_FFLAGS="${FFLAGS}";      unset FFLAGS
  export MPICHLIB_F90FLAGS="${F90FLAGS}";  unset F90FLAGS
  export MPICHLIB_LDFLAGS="${LDFLAGS}";    unset LDFLAGS
  export CC=icc
  export CXX=icpc
  export CPP='icc -E'
  export CXXCPP='icpc -E'
  export FC=ifort
  export F77=ifort
  export CFLAGS='-O3 -xHost -ip -no-prec-div -static-intel'
  export CXXFLAGS='-O3 -xHost -ip -no-prec-div -static-intel'
  export FFLAGS='-O3 -xHost -ip -no-prec-div -static-intel'
  mkdir -p build
  cd build

  ../configure --prefix=/opt/mpich-intel --enable-shared \
    --enable-error-checking=runtime --enable-error-messages=all \
    --enable-timer-type=clock_gettime \
    --with-python=python2

  make
  make mandoc
}

check() {
  cd ${srcdir}/mpich-${pkgver}/build

  # CFLAGS etc are normally written into the wrapper compilers.  This
  # gives surprising results, e.g. when the user wants to compile their
  # program without optimization.
  export MPICHLIB_CFLAGS=${CFLAGS};      unset CFLAGS
  export MPICHLIB_CXXFLAGS=${CXXFLAGS};  unset CXXFLAGS
  export MPICHLIB_CPPFLAGS=${CPPFLAGS};  unset CPPFLAGS
  export MPICHLIB_FFLAGS=${FFLAGS};      unset FFLAGS
  export MPICHLIB_F90FLAGS=${F90FLAGS};  unset F90FLAGS
  export MPICHLIB_LDFLAGS=${LDFLAGS};    unset LDFLAGS

  make check
}

package() {
  cd "${srcdir}/mpich-${pkgver}/build"
  make DESTDIR="${pkgdir}" install

  ln -sf mpiexec.hydra "${pkgdir}/opt/mpich-intel/bin/mpiexec"
  ln -sf mpiexec "${pkgdir}/opt/mpich-intel/bin/mpirun"
  ln -sf mpiexec.hydra "${pkgdir}/opt/mpich-intel/bin/mpirun.hydra"

  install -D -m644 "${srcdir}/mpich-${pkgver}/COPYRIGHT" "${pkgdir}/usr/share/licenses/mpich/license.txt"
  install -D -m755 "${srcdir}/${pkgname}.profile" "${pkgdir}/etc/profile.d/mpich.sh"

  mkdir -p "${pkgdir}/etc/ld.so.conf.d"
  echo /opt/mpich-intel/lib > "${pkgdir}/etc/ld.so.conf.d/mpich.conf"
}
