# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=netcdf-intel-fortran
pkgdirname=netcdf-fortran
pkgver=4.4.4
pkgrel=1
pkgdesc="NetCDF fortran bindings"
arch=("i686" "x86_64")
url="https://github.com/Unidata/netcdf-fortran"
depends=('gcc-libs' 'netcdf' 'curl' 'gcc-fortran')
makedepends=('doxygen')
options=('!makeflags' 'staticlibs')
license=('custom')
#source=(http://www.unidata.ucar.edu/downloads/netcdf/ftp/netcdf-fortran-${pkgver}.tar.gz)
source=("https://github.com/Unidata/netcdf-fortran/archive/v${pkgver}.tar.gz")
sha1sums=('9174a7847e14a35d15188f5230979726a0fd541a')

build() {
  . /opt/intel/composerxe/linux/bin/compilervars.sh intel64
  export CC=icc
  export CXX=icpc
  export CFLAGS='-O3 -xHost -ip -no-prec-div -static-intel'
  export CXXFLAGS='-O3 -xHost -ip -no-prec-div -static-intel'
  export F77=ifort
  export FC=ifort
  export F90=ifort
  export FFLAGS='-O3 -xHost -ip -no-prec-div -static-intel'
  export CPP='icc -E'
  export CXXCPP='icpc -E'
  cd ${srcdir}/${pkgdirname}-${pkgver}
  ./configure --prefix=/opt/netcdf-intel --enable-shared
  make
}

check() {
  cd ${srcdir}/${pkgdirname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgdirname}-${pkgver}

  install -m755 -d ${pkgdir}/opt/netcdf-intel/lib
  install -m755 -d ${pkgdir}/opt/netcdf-intel/share/man
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/${pkgdirname}-${pkgver}/COPYRIGHT \
        ${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT

}
